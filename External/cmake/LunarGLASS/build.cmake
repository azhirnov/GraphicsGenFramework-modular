
set( LUNARGLASS_TARGET "LunarGLASS_Dependencies" )
set( LUNARGLASS_OUTPUT "${CMAKE_BINARY_DIR}/LunarGLASS_bin" )

ExternalProject_Add( "External.LunarGLASS"
		DEPENDS			"External.Download"
		LIST_SEPARATOR	"${EXTERNAL_LIST_SEPARATOR}"
		# configure
		SOURCE_DIR		"${CMAKE_CURRENT_SOURCE_DIR}/LunarGLASS"
		CMAKE_GENERATOR	"${CMAKE_GENERATOR}"
		CMAKE_GENERATOR_TOOLSET	"${CMAKE_GENERATOR_TOOLSET}"
		CMAKE_ARGS		"-DCMAKE_CONFIGURATION_TYPES=${EXTERNAL_CONFIGURATION_TYPES}"
						"-DCMAKE_SYSTEM_VERSION=${CMAKE_SYSTEM_VERSION}"
						"-DCMAKE_DEBUG_POSTFIX="
						"-DCMAKE_RELEASE_POSTFIX="
						"-DCMAKE_LIBRARY_OUTPUT_DIRECTORY=${LUNARGLASS_OUTPUT}"
						"-DCMAKE_ARCHIVE_OUTPUT_DIRECTORY=${LUNARGLASS_OUTPUT}"
						"-DEXTERNALS_PATH=${EXTERNALS_PATH}"
						${EXTERNAL_BUILD_TARGET_FLAGS}
		LOG_CONFIGURE 	1
		# build
		BINARY_DIR		"${LUNARGLASS_OUTPUT}"
		BUILD_COMMAND	"${CMAKE_COMMAND}"
						--build .
						--target ${LUNARGLASS_TARGET}
						--config $<CONFIG>
		LOG_BUILD 		1
		# install
		INSTALL_DIR 	""
		INSTALL_COMMAND ""
		LOG_INSTALL 	1
		# test
		TEST_COMMAND 	""
	)
	
ExternalProject_Get_Property( "External.LunarGLASS" BINARY_DIR )
set_property( TARGET "External.LunarGLASS" PROPERTY FOLDER "External" )

set( LUNARGLASS_DEPS "core" "glslangFrontend" "GLSLBackend" "SpvFrontend" "${LUNARGLASS_TARGET}" )
set( LUNARGLASS_GLSLANG_DEPS "glslang" "HLSL"  "OGLCompiler" "OSDependent" "SPIRV" "SPIRV-Tools" "SPIRV-Tools-opt" "SPVRemapper" )
set( LUNARGLASS_LLVM_DEPS "LLVMAsmParser" "LLVMAnalysis" "LLVMCore" "LLVMInstCombine" "LLVMipa" "LLVMipo" "LLVMLinker" "LLVMSupport" "LLVMScalarOpts" "LLVMTarget" "LLVMTransformUtils" )
set( LUNARGLASS_DEPS_SRC "" )
set( LUNARGLASS_DEPS_DST "" )


if ( ${CONFIGURATION_DEPENDENT_PATH} )
	foreach ( DEP ${LUNARGLASS_DEPS} )
		set( LUNARGLASS_DEPS_SRC "${LUNARGLASS_DEPS_SRC}" "${BINARY_DIR}/$<CONFIG>/${CMAKE_STATIC_LIBRARY_PREFIX}${DEP}${CMAKE_STATIC_LIBRARY_SUFFIX}" )
	endforeach ()
	foreach ( DEP ${LUNARGLASS_GLSLANG_DEPS} )
		set( LUNARGLASS_DEPS_SRC "${LUNARGLASS_DEPS_SRC}" "${BINARY_DIR}/$<CONFIG>/${CMAKE_STATIC_LIBRARY_PREFIX}${DEP}${CMAKE_STATIC_LIBRARY_SUFFIX}" )
	endforeach ()
	foreach ( DEP ${LUNARGLASS_LLVM_DEPS} )
		set( LUNARGLASS_DEPS_SRC "${LUNARGLASS_DEPS_SRC}" "${BINARY_DIR}/LunarGLASS/Core/LLVM/llvm-3.4/lib/$<CONFIG>/${CMAKE_STATIC_LIBRARY_PREFIX}${DEP}${CMAKE_STATIC_LIBRARY_SUFFIX}" )
	endforeach ()
else ()
	foreach ( DEP ${LUNARGLASS_DEPS} )
		set( LUNARGLASS_DEPS_SRC "${LUNARGLASS_DEPS_SRC}" "${BINARY_DIR}/${CMAKE_STATIC_LIBRARY_PREFIX}${DEP}${CMAKE_STATIC_LIBRARY_SUFFIX}" )
	endforeach ()
	foreach ( DEP ${LUNARGLASS_GLSLANG_DEPS} )
		set( LUNARGLASS_DEPS_SRC "${LUNARGLASS_DEPS_SRC}" "${BINARY_DIR}/${CMAKE_STATIC_LIBRARY_PREFIX}${DEP}${CMAKE_STATIC_LIBRARY_SUFFIX}" )
	endforeach ()
	foreach ( DEP ${LUNARGLASS_LLVM_DEPS} )
		set( LUNARGLASS_DEPS_SRC "${LUNARGLASS_DEPS_SRC}" "${BINARY_DIR}/LunarGLASS/Core/LLVM/llvm-3.4/lib/${CMAKE_STATIC_LIBRARY_PREFIX}${DEP}${CMAKE_STATIC_LIBRARY_SUFFIX}" )
	endforeach ()
endif ()


foreach ( SRC ${LUNARGLASS_DEPS_SRC} )
	get_filename_component( DST_NAME "${SRC}" NAME )
	set( DST "${BINARY_DIR}/lib/${DST_NAME}" )
	set( LUNARGLASS_DEPS_DST "${LUNARGLASS_DEPS_DST}" "${DST}" )

	add_custom_command (
		TARGET "External.LunarGLASS" POST_BUILD
		COMMAND ${CMAKE_COMMAND} -E copy_if_different "${SRC}" "${DST}"
		COMMENT "Copying ${DST_NAME} library..."
	)
endforeach ()


add_library( "LunarGLASS" INTERFACE )
add_dependencies( "LunarGLASS" "External.LunarGLASS" )
set_property( TARGET "LunarGLASS" PROPERTY INTERFACE_LINK_LIBRARIES ${LUNARGLASS_DEPS_DST} )

